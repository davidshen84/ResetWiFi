import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

apply plugin: 'com.android.application'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId "org.shen.xi.resetwifi"
    minSdkVersion 23
    targetSdkVersion 23
    versionCode 7
    versionName "2.2.1"

    buildConfigField "boolean", "hasPrivilege", "false"
    buildConfigField "boolean", "hasNetworkHistory", "false"
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  productFlavors {
    full { null }

    fakeRoot1 {
      buildConfigField 'boolean', 'hasPrivilege', 'true'
      buildConfigField 'boolean', 'hasNetworkHistory', 'true'
    }
  }
}

final def log = project.logger
final def variants = project.android.applicationVariants

variants.all { variant ->
  JavaCompile javaCompile = variant.javaCompile
  javaCompile.doLast {
    String[] args = ["-showWeaveInfo",
                     "-1.5",
                     "-inpath", javaCompile.destinationDir.toString(),
                     "-aspectpath", javaCompile.classpath.asPath,
                     "-d", javaCompile.destinationDir.toString(),
                     "-classpath", javaCompile.classpath.asPath,
                     "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
    log.info "ajc args: " + Arrays.toString(args)

    MessageHandler handler = new MessageHandler(true);
    new Main().run(args, handler);
    for (IMessage message : handler.getMessages(null, true)) {
      switch (message.getKind()) {
        case IMessage.ABORT:
        case IMessage.ERROR:
        case IMessage.FAIL:
          log.error message.message, message.thrown
          break;
        case IMessage.WARNING:
          log.warn message.message, message.thrown
          break;
        case IMessage.INFO:
          log.info "ajc: " + message.message, message.thrown
          break;
        case IMessage.DEBUG:
          log.debug "ajc: " + message.message, message.thrown
          break;
      }
    }
  }
}

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.0.44-beta'
  compile 'com.android.support:appcompat-v7:23.2.1'
  compile 'com.google.android.gms:play-services-analytics:8.4.0'
  compile 'com.google.inject:guice:4.0:no_aop'
  compile 'com.google.guava:guava:19.0'
  compile 'eu.chainfire:libsuperuser:1.0.0.201602011018'
  compile 'org.aspectj:aspectjrt:1.8.8'
}
